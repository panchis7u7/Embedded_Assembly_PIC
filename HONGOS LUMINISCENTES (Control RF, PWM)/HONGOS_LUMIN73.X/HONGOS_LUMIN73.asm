 ;PROCESSOR P16F73
 #INCLUDE <P16F73.INC>
 LIST P=16F73
; CONFIG
; __config 0xFFF2
 __CONFIG _FOSC_HS & _WDTE_OFF & _PWRTE_ON & _CP_OFF & _BOREN_ON
 
 #DEFINE RX_BYTE 0x01
 
 CBLOCK 0x20
    CONTADOR
    UART
    GUARDA_W
    GUARDA_STATUS
    TEMP
    CONTADOR_TIEMPO
 ENDC
 
    ORG 0
    GOTO SETUP
    ORG 4
    GOTO INTERRUPCION
SETUP:
    BSF STATUS,RP0	    ;BANCO 1.
    BCF STATUS, RP1	    
    MOVLW B'00100000'	    ;HABILITA LA INTERRUPCION PARA RX (RECEPCION SERIAL) EN EUSART.
    MOVWF PIE1		    ;HABILITACION DE INTERRUPCION PARA RX.
    CLRF PIE2
    MOVLW B'10000000'	    ;PUERTO RX COMO ENTRADA Y CCP1,CCP2/PWM 1KHz.
    MOVWF TRISC
    MOVLW B'00100100'
    MOVWF TXSTA
    MOVLW D'207'	    ;RESULTADO DEL CALCULO PARA 2400 BAUDIOS.
    MOVWF SPBRG
    CLRF TRISB		    ;SALIDA PARA LOS LEDS.
    MOVLW D'249'
    MOVWF PR2
    BCF STATUS,RP0	    ;BANCO 0.
    BCF STATUS,RP1	    ;CONFIGURACION DEL PUERTO RX.
    CLRF T1CON
    CLRF PIR1
    CLRF PIR2
    MOVLW B'10010000'
    MOVWF RCSTA
    MOVLW B'11000000'	    ;CONFIGURACION PARA INTERRUPCION RX.
    MOVWF INTCON	    ;INTERRUPCION EUSART, PEIE Y GIE.
    MOVLW B'00001100'
    MOVWF CCP1CON
    MOVWF CCP2CON
    MOVLW D'204'
    MOVWF CCPR1L
    MOVLW D'0'
    MOVWF CCPR2L
    MOVLW B'00000101'
    MOVWF T2CON
    CLRF PORTB
    MOVF RCREG,W	    ;Flush receive buffer
    MOVF RCREG,W
    MOVF RCREG,W
    MOVLW 0x00
    MOVWF CONTADOR
    GOTO PROGRAMA
    
	;ESPERA AQUI HASTA QUE EL USUARIO HAGA UNA DESICION DEL MENU.
	;------------------------------------------------------------
ESPERA_CMD:
    BCF UART,RX_BYTE
ESPERA_CMD_NUEVO:
    BTFSC UART,RX_BYTE
    GOTO PROGRAMA
    GOTO ESPERA_CMD_NUEVO
	;MENU DE DESICIONES.
	;------------------------------------------------------------
    
PROGRAMA:
    BANKSEL TEMP
    MOVFW TEMP
    XORLW 0x55
    BTFSC STATUS,Z
    GOTO CAMBIO_COLOR
    
    MOVFW TEMP
    XORLW 0xAA
    BTFSC STATUS,Z
    GOTO CAMBIO_DUTY
    
    MOVFW TEMP
    XORLW 0x33
    BTFSC STATUS,Z
    GOTO CAMBIO_AUTO
    CLRF TEMP
    GOTO ESPERA_CMD

CAMBIO_COLOR:
    MOVLW D'255'
    MOVWF CCPR2L
    MOVLW D'1'
    ADDWF CONTADOR,1
    MOVFW CONTADOR
    ANDLW D'7'
    MOVWF PORTB
    GOTO ESPERA_CMD
    
CAMBIO_DUTY:
    MOVLW D'51'
    ADDWF CCPR1L,1
    GOTO ESPERA_CMD
    
CAMBIO_AUTO:
    BCF UART,RX_BYTE
    MOVLW 0x00
    MOVWF CCPR2L
REPETICION:
    MOVLW D'1'
    ADDWF CONTADOR,1
    MOVFW CONTADOR
    ANDLW D'7'
    MOVWF PORTB
    MOVLW 0xFF
    MOVWF CONTADOR_TIEMPO
LED_BRILLO:
    BTFSC UART,RX_BYTE
    GOTO ESPERA_CMD_NUEVO
    CALL CAMBIO_RETARDO_15MS
    INCF CCPR2L,1
    DECFSZ CONTADOR_TIEMPO,F
    GOTO LED_BRILLO
    CALL CAMBIO_RETARDO_15MS
    MOVLW 0xFF
    MOVWF CONTADOR_TIEMPO
LED_APAGADO:
    BTFSC UART,RX_BYTE
    GOTO ESPERA_CMD_NUEVO
    CALL CAMBIO_RETARDO_15MS
    DECF CCPR2L,1
    DECFSZ CONTADOR_TIEMPO,F
    GOTO LED_APAGADO
    GOTO REPETICION
  
CAMBIO_RETARDO_15MS:   
    BCF T1CON,TMR1ON
    MOVLW 0x65
    MOVWF TMR1H
    MOVLW 0x68
    MOVWF TMR1L
    BSF T1CON,TMR1ON
 RETARDO_15MS_4_0:
    BTFSS PIR1,TMR1IF
    GOTO RETARDO_15MS_4_0
    BCF T1CON,TMR1ON
    BCF PIR1,TMR1IF
    RETURN
    
INTERRUPCION:
    MOVWF GUARDA_W
    SWAPF STATUS,W
    MOVWF GUARDA_STATUS
    BCF STATUS,RP0
    BCF STATUS,RP1
GUARDADO:
    BTFSS PIR1,RCIF	    ;CHECAR SI LA INTERRUPCION ES DEL USART.
    GOTO EXIT 
    BTFSC UART,RX_BYTE	    ;SI RX_BIT ESTA ALTO, ENTONCES RXREG NO SE HA PROCESADO AUN.
    GOTO EXIT
    MOVFW RCREG		    ;Read input byte into W
    MOVWF TEMP		    ;Store received byte to shared RAM
    BSF UART,RX_BYTE
    MOVFW RCREG		    ;PUEDE HABER UN SEGUNDO BYTE.
    BCF RCSTA,CREN
    BSF RCSTA,CREN
EXIT:
    MOVFW RCREG
    MOVFW RCREG
    SWAPF GUARDA_STATUS,W
    MOVWF STATUS
    SWAPF GUARDA_W,F
    SWAPF GUARDA_W,W
    RETFIE
    END

